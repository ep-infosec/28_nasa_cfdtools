      SUBROUTINE D0GBLE ( A,NROW,N,ML,MU,B,WORK,NWORK,RCOND,IER )
         INTEGER NROW, N, ML, MU, NWORK, IER
         DOUBLE PRECISION A(NROW,*), B(*), RCOND, WORK(*)
C
C*********************************************************************
C
C PURPOSE   D0GBLE SOLVES AX=B FOR X WHERE A IS AN N BY N DOUBLE REAL
C           GENERAL, BANDED MATRIX.  D0GBLE ALLOWS ONLY A SINGLE RIGHT
C           HAND SIDE VECTOR B- MULTIPLE RIGHT HAND SIDES CAN BE
C           SOLVED ECONOMICALLY BY USING DTGBSL IN CONJUNCTION WITH
C           D0GBLE.
C
C METHOD    D0GBLE USES LINPACK SUBROUTINE DGBCO WHICH USES GAUSSIAN
C           ELIMINATION WITH PARTIAL PIVOTING TO COMPUTE THE LU
C           FACTORIZATION OF A WHERE L IS AN ELEMENTARY LOWER
C           TRIANGULAR MATRIX AND U IS AN UPPER TRIANGULAR MATRIX.
C           AN ESTIMATE OF THE RECIPROCAL CONDITION NUMBER, RCOND,
C           IS THEN COMPUTED.  IF THE MATRIX A IS NOT SINGULAR LINPACK
C           SUBROUTINE DGBSL IS CALLED TO COMPUTE THE SOLUTION X.
C
C REMARK    PLEASE SEE THE SUBROTINE ABSTRACT OR LINPACK
C           DOCUMENTATION FOR A DESCRIPTION OF BAND STORAGE STRUCTURE
C           USED IN THIS SUBROUTINE.
C
C           TO COMPUTE ADDITIONAL SOLUTIONS INVOLVING THE SAME MATRIX
C           A BUT DIFFERENT RIGHT HAND SIDES FOLLOW THE USE OF D0GBLE
C           WITH SUBROUTINE DTGBSL.  FOR POSSIBLE ECONOMIES OF STORAGE
C           AND/OR COMPUTATION, FOR APPLICABILITY OF MORE SPECIALIZED
C           ROUTINES, OR FOR INVERSES OR DETERMINANTS OF MATRICES, SEE
C           INTRODUCTION TO LINEAR ALGEBRA SECTION.
C
C USAGE     DOUBLE PRECISION  A(NROW,N), B(N), WORK(2*N)
C           CALL D0GBLE ( A,NROW,N,ML,MU,B,WORK,NWORK,RCOND,IER )
C
C INPUT     A         DOUBLY SUBSCRIPTED ARRAY WHICH CONTAINS THE
C                     MATRIX.
C
C           NROW      ROW DIMENSION OF THE ARRAY A WHICH MUST BE
C                     AT LEAST 2*ML+MU+1.  (SEE INTRODUCTION TO LINEAR
C                     ALGEBRA SECTION FOR FURTHER DETAILS)
C
C           N         ORDER OF THE MATRIX A AND THE LENGTH OF THE
C                     RIGHT HAND SIDE COLUMN VECTOR B.
C
C           ML        LOWER BAND WIDTH, 0 .GE. ML .LT. N.
C
C           MU        UPPER BAND WIDTH, 0. GE. MU .LT. N.
C
C           B         ARRAY WHICH CONTAINS THE RIGHT HAND SIDE
C                     COLUMN VECTOR B.
C
C WORKING   WORK      WORK VECTOR OF LENGTH NWORK.  THIS VECTOR
C STORAGE             SHOULD BE PRESERVED IF OTHER LINEAR SYSTEMS
C                     WITH THE SAME COEFFICIENT MATRIX ARE TO BE
C                     SOLVED (SEE DTGBSL).
C
C           NWORK     THE LENGTH OF THE VECTOR WORK WHICH MUST BE AT
C                     LEAST 2*N.
C
C  OUTPUT   A         IF IER .GE. 0 OR IER = -4, THEN A HAS BEEN
C                     OVERWRITTEN WITH ITS L*U FACTORIZATION.
C                     OTHERWISE A IS UNCHANGED.  THIS ARRAY SHOULD
C                     BE PRESERVED IF OTHER LINEAR SYSTEMS WITH THE
C                     SAME COEFFICIENT MATRIX ARE TO BE SOLVED (SEE
C                     DTGBSL).
C
C           B         IF IER .GE. 0, THEN B HAS BEEN OVERWRITTEN
C                     WITH THE COMPUTED SOLUTION.  OTHERWISE IT IS
C                     UNCHANGED.
C
C           RCOND     ESTIMATE OF THE RECIPROCAL OF THE CONDITIN
C                     NUMBER OF THE MATRIX A.  IF RCOND IS SO SMALL
C                     THAT THE LOGICAL EXPRESSION  1.0 + RCOND = 1.0
C                     IS TRUE, THEN A CAN USUALLY BE REGARDED AS
C                     SINGULAR TO WORKING PRECISION.  (SEE INTRO-
C                     DUCTION TO LINEAR ALGEBRA SECTION FOR FURTHER
C                     DETAILS.)
C
C           IER       SUCCESS/ERROR CODE WHICH COMMUNICATES TO THE
C                     USER SUCCESS, WARNINGS, OR ERRORS.  POSSIBLE
C                     RETURN VALUES ARE
C
C                     IER =  0, NORMAL RETURN
C                         =  1, SOLUTION HAS BEEN COMPUTED BUT A
C                               IS POORLY CONDITIONED
C                         =  2, SOLUTION HAS BEEN COMPUTED BUT A
C                               IS BADLY CONDITIONED
C                         = -1, N IS LESS THAN 1
C                         = -2, NROW IS LESS THAN 2*ML + MU + 1
C                         = -3, NWORK IS LESS THAN 2*N
C                         = -4, MATRIX IS EXACTLY SINGULAR, NO
C                               WAS COMPUTED.
C                         = -6, ML .GE. N   OR   ML .LT. 0
C                         = -7, MU .GE. N   OR   MU .LT. 0
C
C
C  WRITTEN BY ROGER G. GRIMES ON SEPTEMBER 20, 1979
C ...        MODIFIED BY KAREN J. EKBLAD DECEMBER 1985
C
C*********************************************************************
C
C ... EXTERNAL SUBROUTINES CALLED ARE
C
C           DLOG10, DTMCON, DTERR, DGBCO, DGBSL
C
C
C ... INTERNAL VARIABLES
C
      DOUBLE PRECISION DIGCAL, DIGMAX, MACHEP, DLOG10, DTMCON
      CHARACTER*8 NAME
      INTEGER IPVT, NEED
C
C ... INITIALIZE THE ERROR MESSAGE ARRAYS
C
C
       DATA NAME /'D0GBLE'/
C
C ... TEST INITIAL PARAMETERS
C
         IER = 0
         RCOND = 0.D0
         IF (N .GE. 1) GO TO 3
            IER = -1
            CALL DTERR ( 1, NAME, IER, 1 )
            GO TO 70
C
   3     IF ( ML .GE. 0 .AND. ML .LT. N ) GO TO 6
            IER = -6
            CALL DTERR ( 1, NAME, IER, 1 )
            GO TO 70
C
   6     IF ( MU .GE. 0 .AND. MU .LT. N ) GO TO 10
            IER = -7
            CALL DTERR ( 1, NAME, IER, 1 )
            GO TO 70
C
  10     IF ( NROW .GE. 2*ML + MU + 1 ) GO TO 20
            IER = -2
            CALL DTERR ( 1, NAME, IER, 1)
            GO TO 70
C
  20     IF ( NWORK .GE. 2*N ) GO TO 30
            NEED = 2*N
            IER = -3
            CALL DTERR ( 2, NAME, IER, NEED )
            GO TO 70
C
C ... SET WORKSPACE BASE ADDRESS AND THEN FACTOR A
C
  30     IPVT = 1 + N
         CALL DGBCO ( A,NROW,N,ML,MU,WORK(IPVT),RCOND,WORK )
C
C ... INITIALIZE WORK(1) SO THAT ERROR MESSAGE VALUES CAN BE STORED
C
         WORK(1) = 0.D0
C
C ... TEST FOR EXACT SINGULARITY AND ILL CONDITIONING OF THE
C ... MATRIX
C
         IF ( RCOND .NE. 0.D0 ) GO TO 40
            IER = -4
            CALL DTERR ( 3, NAME, IER, 1 )
            GO TO 70
C
  40     MACHEP = DTMCON ( 5 )
         DIGMAX = -DLOG10 ( MACHEP )
         DIGCAL = -DLOG10 ( RCOND )
         IF ( 3.D0 * DIGCAL .LT.         DIGMAX ) GO TO 60
         IF ( 3.D0 * DIGCAL .LT. 2.D0 * DIGMAX ) GO TO 50
C
C ... MATRIX IS BADLY CONDITIONED
C
            IER = 2
            CALL DTERR( 0, NAME, IER, 1 )
            CALL D0SINT ( WORK, 1, IER )
            GO TO 60
C
C ... MATRIX IS POORLY CONDITIONED
C
  50        IER = 1
            CALL DTERR ( 0, NAME, IER, 1 )
            CALL D0SINT ( WORK, 1, IER )
C
C ... SOLVE THE MATRIX EQUATIONS
C
  60     CALL DGBSL ( A,NROW,N,ML,MU,WORK(IPVT),B,0 )
C
C ... END OF THE SUBROUTINE
C
  70     CONTINUE
         RETURN
      END

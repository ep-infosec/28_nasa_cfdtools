C
      SUBROUTINE DTLCMB (CSPL, DSPL, ALPHA, BETA, GAMMA, ESPL, IER)
C***********************************************************************
C
C FUNCTION-
C           LINEAR COMBINATION OF TWO COMPATIBLE SPLINES
C
C AUTHORS-
C          C.P.CHI             CREATION  OCTOBER   1989
C
C INPUT-
C          CSPL   = ARRAY CONTAINING DATA FOR THE FIRST SPLIN
C          DSPL   = ARRAY CONTAINING DATA FOR THE SECOND SPLINE
C          ALPHA  = CONSTANT FOR MODIFY THE FIRST SPLINE
C          BETA   = CONSTANT FOR MODIFY THE SECOND SPLINE
C          GAMMA  = OFFSET OF THE OUTPUT SPLINE
C
C OUTPUT-
C          ESPL   = ARRAY CONTAINING DATA FOR THE COMBINED SPLINES
C          IER    = ERROR FLAG
C                 = 0   NO ERROR
C                 =-1   NUMBER OF INDEPENDENT VARIABLE NOT ACCEPTABLE
C                 =-2   NUMBER OF DEPENDENT VARIABLE NOT ACCEPTABLE
C                 =-3   ORDER OF THE SPLIN NOT ACCEPTABLE
C                 =-4   INCORRECT NUMBER OF KNOTS
C                 =-5   INCORRECT KNOT VECTOR
C                       MULTIPLICITY AT ENDS NOT CORRECT
C                       NOT IN ASCENDING ORDER
C                 =-62  INTEGER PARAMETERS OF THE SPLINES NOT EQUAL
C                 =-63  KNOT VECTORS OF THE SPLINE NOT EQUAL
C                 =-64  DENOMINATORS OF THE SPLINES NOT EQUAL
C                 =-65  ENTITY IS NEITHER A CURVE NOR A SURFACE
C
C
C TYPE/PARAMETER/DIMENSION/COMMON/EQUIVALENCE/DATA/FORMAT STATEMENTS-
C
      DOUBLE PRECISION CSPL(*), DSPL(*), ESPL(*), GAMMA(*)
      DOUBLE PRECISION ALPHA, BETA
C
C ********************** START OF EXECUTABLE CODE **********************
C
      IER=0
C
C...CHECK DATA OF THE INPUT SPLINES
C
      CALL DTSCHK(CSPL, IER)
      IF(IER.NE.0) GOTO 999
C
      CALL DTSCHK(DSPL, IER)
      IF(IER.NE.0) GOTO 999
C
C...CHECK COMPATIBILITY OF THE INPUT SPLINES
C
C...CHECK INTEGER PARAMETERS
C
      IF ( CSPL( 1 ) .GT. 2.) THEN
        IER = -65
        GO TO 999
      ENDIF
      IH = CSPL(1)*3+2
      IH1= IH - CSPL(1)
C
C               CHECK INTEGERS EXCEPT THE POINTERS TO THE 
C               LAST EVALUATION INTERVAL: THESE DO NOT NEED
C               TO BE THE SAME
C
      DO 100 I=1, IH1
      IF(CSPL(I).NE.DSPL(I)) THEN
        IER=-62
        GOTO 999
      ENDIF
  100 CONTINUE
C
C...CHECK KNOT VECTORS
C ***
      IF(CSPL(1).EQ.1) THEN
C ***
C...SPLINES ARE TWO CURVES
C
        NK1 = CSPL(3) + CSPL(4)
        IKF = IH + 1
        IKL = IH + NK1
C
        DO 200 I=IKF, IKL
        IF(CSPL(I).NE.DSPL(I)) THEN
          IER=-63
          GOTO 999
        ENDIF
  200   CONTINUE
C ***
      ELSE
C ***
C
C...SPLINES ARE TWO SURFACES
C
C...CHECK KNOT VECTOR IN THE DIRECTION OF THE FIRST INDEPENDENT
C   VARIABLE
C
        NK1 = CSPL(3) + CSPL(5)
        IKF = IH + 1
        IKL = IH + NK1
C
        DO 400 I=IKF, IKL
        IF(CSPL(I).NE.DSPL(I)) THEN
          IER=-63
          GOTO 999
        ENDIF
  400   CONTINUE
C
C...CHECK KNOT VECTOR IN THE DIRECTION OF THE SECOND INDEPENDENT
C   VARIABLE
C
        NK2 = CSPL(4) + CSPL(6)
        IKF = IH + NK1 + 1
        IKL = IH + NK1 + NK2
C
        DO 500 I=IKF, IKL
        IF(CSPL(I).NE.DSPL(I)) THEN
          IER=-63
          GOTO 999
        ENDIF
  500   CONTINUE
C ***
      ENDIF
C ***
C
C...NUMBER OF CONTROL POINTS
C
      IF(CSPL(1).EQ.1) THEN
        NCTRP=DINT(CSPL(4))
      ELSE
        NCTRP=DINT(CSPL(5)*CSPL(6))
      ENDIF
C
C...NUMBER OF COMPONENTS IN EACH CONTROL POINT
C
      NCOEF=DINT(CSPL(2))
      IF(NCOEF.LT.0) NCOEF=-NCOEF
C
C...CHECK WEIGHTS (DENOMINATORS)
C
      IF(CSPL(2).LT.0) THEN
        IWTF=IKL+(NCOEF-1)*NCTRP+1
        IWTL=IWTF+NCTRP-1
C
        DO 600 I=IWTF, IWTL
        IF(CSPL(I).NE.DSPL(I)) THEN
          IER=-64
          GOTO 999
        ENDIF
  600   CONTINUE
      ENDIF
C
C...START THE LINEAR COMBINATION
C
C   COPY INTEGER PARAMETER, KNOT VECTOR, AND WEIGHT
C
      DO 700 I=1, IKL
      ESPL(I)=CSPL(I)
  700 CONTINUE
C
      IF(CSPL(2).LT.0) THEN
        DO 750 I=IWTF, IWTL
        ESPL(I)=CSPL(I)
  750   CONTINUE
      ENDIF
C
C...LINEAR COMBINATION OF SPLINE COEF
C
      ICF=IKL+1
      IF(CSPL(2).LT.0) GO TO 850
        NRNG = CSPL(2)
        NOW = IKL
        DO 800 I = 1, NRNG
        DO 800 J = 1, NCTRP
        NOW = NOW + 1
        ESPL(NOW) = ALPHA * CSPL(NOW) + BETA * DSPL(NOW)
     *            + GAMMA(I)
  800 CONTINUE
        GO TO 999
850     CONTINUE
        NRNG = -CSPL(2) - 1
        NOW = IKL
        DO 900 I = 1, NRNG
        NOW2 = IWTF - 1
        DO 900 J = 1, NCTRP
        NOW = NOW + 1
        NOW2 = NOW2 + 1
        ESPL(NOW) = ALPHA * CSPL(NOW) + BETA * DSPL(NOW)
     *            + GAMMA(I) * CSPL(NOW2)
900     CONTINUE
  999 CONTINUE
      RETURN
      END

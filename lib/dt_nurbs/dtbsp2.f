      SUBROUTINE DTBSP2 ( XKNOTS, X, IPOS, IK, K, WK1, WK2, VAL )
C
C  PURPOSE  DTBSP2 COMPUTES THE VALUES OF THE POSSIBLY NONZERO
C           B-SPLINES OF ORDER  K  AT  X  FOR THE KNOT SET  XKNOTS.
C           DTBSP2 IS INTENDED AS A LOWER LEVEL MATH LIBRARY ROUTINE
C           AND DOES NOT PERFORM ANY ARGUMENT VALIDITY CHECKING.
C
C  METHOD   DTBSP2 USES THE DE BOOR RECURRANCE RELATION WHICH
C           OBTAINS THE VALUES FOR ORDER  K  FROM THE VALUES FOR ORDER
C           K-1.  THE RECURSION IS PERFORMED IN PLACE SO THAT ONLY THE
C           VALUES FOR ORDER  K  ARE RETURNED.  HOWEVER, DTBSP2 CAN
C           BE CALLED REPEATEDLY TO CALCULATE THE VALUES FOR MULTIPLE
C           ORDERS WITH EACH NEW CALCULATION STARTING FROM THE LAST
C           ORDER CALCULATED.
C
C  REF.     DTBSP2 IS BASED ON DE BOOR SUBROUTINE BSPLVB.  SEE PAGE 134
C           IN DE BOOR, A PRACTICAL GUIDE TO SPLINES, SPRINGER-VERLAG,
C           1978.
C
C  USAGE    DOUBLE PRECISION XKNOTS(NKNOTS), WK1(K-1), WK2(K-1), VAL(K)
C           CALL DTBSP2 (XKNOTS, X, IPOS, IK, K, WK1, WK2, VAL)
C
C  INPUT    XKNOTS  ARRAY OF LENGTH  NKNOTS  CONTAINING THE KNOTS.
C
C           X       POINT AT WHICH EVALUATION IS DESIRED.
C
C           IPOS    POSITION OF  X  WITHIN KNOTS.  IPOS IS SUCH THAT
C                        XKNOTS(IPOS) .LT. XKNOTS(IPOS+1)
C                   AND  XKNOTS(IPOS) .LE. X .LE. XKNOTS(IPOS+1)
C                   AND  K .LE. IPOS .LE. NKNOTS-K+1.
C
C           IK      INITIAL/CONTINUE FLAG WHICH IS ALSO THE LAST ORDER
C                   CALCULATED.  IF  IK .LE. 0, THE CALCULATION STARTS
C                   FROM SCRATCH.  IF  IK .GT. 0, THE CALCULATION STARTS
C                   FROM ORDER  IK  AND IS CONTINUED TO ORDER  K.  FOR
C                   A CONTINUE CALL ONLY THE VALUE OF  K  CAN BE CHANGED
C                   FROM THE PREVIOUS CALL.
C
C           K       ORDER OF THE B-SPLINES TO BE COMPUTED,
C                   K .GT. MAX(0,IK).
C
C  WORKING  WK1,    WORKING STORAGE ARRAYS OF LENGTH AT LEAST  K-1.
C  STORAGE  WK2
C
C  OUTPUT   IK      SET FOR CONTINUATION, IK = K.
C
C           VAL  VALUES OF THE  K  POSSIBLY NONZERO B-SPLINES AT X.
C
C  CALLS    NONE.
C
C  AUTHOR   LYNN T. WINTER.
C
C  DATE     4 - JAN - 1985.
C
C ====================================================================
C
C ... ARGUMENT DECLARATIONS.
C
      INTEGER IPOS, IK, K
      DOUBLE PRECISION    XKNOTS(*), X,  WK1(*), WK2(*), VAL(*)
C
C ... LOCAL VARIABLE DECLARATIONS.
C
      INTEGER IL1, L
      DOUBLE PRECISION    VM, VMPREV
C
C ====================================================================
C
C ... CHECK FOR INITIAL/CONTINUE CALL.
C
      IF ( IK .GT. 0 ) GO TO 10
      IK = 1
      VAL(1) = 1.D0
      IF ( K .EQ. 1 ) GO TO 30
C
C ... CALCULATIONS FOR ORDER GREATER THAN 1.
C
   10 IL1 = IPOS + IK
      WK2(IK) = XKNOTS(IL1) - X
      IL1 = IPOS - IK + 1
      WK1(IK) = X - XKNOTS(IL1)
      VMPREV = 0.D0
      DO 20 L = 1, IK
          IL1 = IK - L + 1
          VM = VAL(L) / ( WK2(L) + WK1(IL1) )
          VAL(L) = VM * WK2(L) + VMPREV
          VMPREV = VM * WK1(IL1)
   20 CONTINUE
      VAL(IK+1) = VMPREV
      IK = IK + 1
      IF ( IK .LT. K ) GO TO 10
C
   30 RETURN
C
      END

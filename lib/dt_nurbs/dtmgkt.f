      SUBROUTINE DTMGKT(CIN,ICPTR,N,T,MLT,NT,
     1                  NDIM,WORK,NWORK,
     2                  COUT,LCOUT,IER)
C+
C  PURPOSE:  DTMGKT PREPARES INPUT DATA SETS FOR THE DTSTFI
C            SURFACE CONSTRUCTION ROUTINE.  REFER TO DOCUMENTATION
C            FOR DTSTFI WHILE READING THE FOLLOWING.
C
C            DTMGKT TAKES IN A SET OF C-VECTORS DEFINING
C            SPLINE CURVES OF THE SAME POLYNOMIAL DEGREE,
C            BUT POSSIBLY WITH DIFFERENT KNOT SETS,
C            AND AN OPTIONAL USER-SPECIFIED COLLECTION OF
C            ADDITIONAL KNOT VALUES AND MULTIPLICITIES.
C
C            DTMGKT RETURNS AN ARRAY OF C-VECTORS
C            EXPRESSING THE SAME UNDERLYING FUNCTIONS,
C            BUT WITH A UNIFORM KNOT SET WHICH INCLUDES
C            THE USER-SPECIFIED KNOTS WITH THE SPECIFIED
C            MULTIPLICITIES.
C
C            THE INPUT C-VECTORS ARE ASSUMED TO BE OUTPUT FROM SOME
C            SPLINE CONSTRUCTION ROUTINE, E.G., DTPITG.  DTUPDG MAY
C            BE USED IF NECESSARY TO RAISE THE DEGREES OF SOME
C            CURVES SO THAT ALL HAVE THE SAME POLYNOMIAL DEGREE.
C            NOTE THAT DTSTFI REQUIRES THIS COMMON DEGREE TO BE ODD.
C
C            THE INPUT C-VECTORS  REPRESENT ONE FAMILY (EITHER U-CURVES
C            OR V-CURVES) OF THE INPUT DATA CURVES REQUIRED BY DTSTFI.
C            A NORMAL APPLICATION WILL REQUIRE RUNNING DTMGKT TWICE,
C            ONCE FOR THE U-CURVES AND ONCE FOR THE V-CURVES, BEFORE
C            RUNNING DTSTFI.
C
C            FOR THE FIRST CALL, TO PREPARE THE U-CURVES, THE
C            KNOT VALUES T(*) SHOULD BE THE U-PARAMETER VALUES AT WHICH
C            V-CURVES WILL BE SPECIFIED.  THE MULTIPLICITY MLT(I)
C            IS THE NUMBER OF VALUE AND DERIVATIVE DATA V-CURVES
C            THAT WILL BE SPECIFIED AT U = T(I).  FOR THE SECOND CALL,
C            TO PREPARE THE V-CURVES, T(*) SHOULD BE BE V-PARAMETER
C            VALUES AT WHICH U-CURVES ARE SPECIFIED, AND MLT(I) IS
C            THE NUMBER OF U-CURVES SPECIFIED AT V = T(I).
C
C  METHOD:   DTMGKT MERGES THE KNOT SETS OF THE INPUT SPLINES
C            AND THE USER-INPUT KNOT VALUES, AND CALLS
C            DTOSLO TO ADD KNOTS TO CURVES WHEN NECESSARY.
C
C  USAGE:    DOUBLE PRECISION CIN(MC),T(NT),COUT(NDIM,N),WORK(NWORK)
C            INTEGER ICPTR(N),N,NT,MLT(NT),NWORK
C            INTEGER NDIM,LCOUT,IER
C            CALL DTMGKT(CIN,ICPTR,N,T,MLT,NT,NDIM,WORK,NWORK,
C                        COUT,LCOUT,IER)
C
C  INPUTS:   CIN    REAL ARRAY CONTAINING INPUT C VECTORS.
C                   ALL C VECTORS MUST SPECIFY CURVES (NDOM=1)
C                   WITH THE SAME NUMBER OF DEPENDENT VARIABLES (NRNG),
C                   OF THE SAME POLYNOMIAL DEGREE,
C                   DEFINED OVER THE SAME PARAMETER INTERVAL.
C                   (IT IS ASSUMED THAT ALL SPLINES HAVE KNOTS OF FULL
C                    MULTIPLICITY AT THEIR ENDPOINTS.)
C            ICPTR  ARRAY OF POINTERS INTO CIN ARRAY.
C                   THE I'TH C VECTOR BEGINS AT CIN(ICPTR(I)).
C            N      NUMBER OF C VECTORS IN CIN(*).
C            T      ADDITIONAL PARAMETER VALUES TO BE ADDED AS KNOTS.
C                   THESE NEED NOT DEFINE A COMPLETE KNOT SET,
C                   IN PARTICULAR, THE FIRST AND LAST T VALUES NEED
C                   NOT BE THE UPPER AND LOWER LIMITS OF THE COMMON
C                   PARAMETER DOMAIN OF THE INPUT CURVES.
C                   HOWEVER, THEY MUST BE LISTED IN INCREASING ORDER,
C                   I.E., T(I) .LT. T(I+1).
C            MLT    MULTIPLICITIES CORRESPONDING TO KNOTS VALUES IN T(*)
C                   IRRESPECTIVE OF THE KNOTS IN THE INPUT C VECTORS,
C                   THE OUTPUT KNOT SET WILL CONTAIN T(I) WITH
C                   MULTIPLICITY MLT(I), UNLESS MLT(I) IS GREATER THAN
C                   (DEGREE OF OUTPUT SPLINES) + 1.
C            NT     NUMBER OF T AND MLT VALUES.
C            NDIM   FIRST DIMENSION OF COUT ARRAY,
C                   NDIM .GE. EXPECTED LENGTH OF OUTPUT C VECTORS.
C
C                   A SAFE, BUT POSSIBLY VERY LARGE VALUE OF NDIM IS
C                        NDIM = 5 + K + (NRNG+1)*NCPTOT,
C                   WHERE NRNG = CIN(2) IS THE NUMBER OF DEPENDENT
C                   VARIABLES IN EACH CURVE, AND K AND NCPTOT ARE
C                   AS DEFINED IN THE DISCUSSION OF WORKING STORAGE BELO
C
C                   IF DTMGKT RETURNS IER = -61 BECAUSE NDIM IS SET
C                   TOO SMALL, THE OUTPUT VALUE OF LCOUT MAY BE USED
C                   TO SET A SAFE VALUE OF NDIM ON A SUBSEQUENT ATTEMPT.
C
C  WORKING  WORK    REAL WORK ARRAY OF LENGTH NWORK.
C  STORAGE  NWORK   LENGTH OF WORK ARRAY,
C                   NWORK .GE. NDIMP + MAX((K+1)*NCPTOT,N)
C                     WHERE K = CIN(3) = COMMON ORDER OF INPUT SPLINES,
C                     AND NCPTOT = TOTAL NUMBER OF B-SPLINE COEFFICIENTS
C                     PER CURVE EXPECTED ON OUTPUT.
C
C                     NCPTOT = NKTOT - K, WHERE NKTOT IS TOTAL NUMBER
C                     OF KNOTS IN MERGED KNOT SETS, COUNTING MULTIPLICIT
C                     NCPTOT IS NEVER GREATER THAN THE SUM
C                     CIN(ICPTR(1)+3)+...+CIN(ICPTR(N)+3) +
C                                        MLT(1)+...+MLT(NT).
C
C
C  OUTPUT   COUT    ARRAY OF OUTPUT C VECTORS.
C                   COUT(1..LCOUT,I) IS THE I'TH OUTPUT VECTOR.
C           LCOUT   COMMON LENGTH OF ALL OUTPUT C VECTORS.
C           IER     SUCCESS / ERROR CODE.
C                    0  == SUCCESS
C                   -1  == ORDER OF INPUT SPLINES .LT. 1
C                   -2  == N .LT. 1
C                   -3  == NWORK TOO SMALL.
C                   -6  == AN INPUT SPLINE HAS NUMBER OF COEFFICIENTS
C                          LESS THAN ITS ORDER.
C                   -8  == T(*) OUT OF ORDER, OR SOME ELT OF MLT(*)
C                          TOO LARGE.
C                   -51 == NDOM .GT. 1
C                   -52 == NRNG'S NOT ALL EQUAL
C                   -61 == NDIM TOO SMALL
C                   -64 == INPUT CURVES HAVE DIFFERENT DEGREES.
C                   -65 == INPUT CURVES HAVE DIFFERENT PARAMETER
C                          INTERVALS, OR ONE OF THE EXTRA KNOTS T(*)
C                          LIES OUTSIDE THE COMMON PARAMETER INTERVAL.
C                          OTHERWISE, AN UNEXPECTED ERROR IN DTOSLO.
C
C  SUBROUTINES CALLED:  DTOSLO
C
C  DATE:  31-JAN-86
C-
      DOUBLE PRECISION CIN(*),T(*),COUT(*),WORK(*)
      INTEGER ICPTR(*),N,MLT(*),NT,NDIM,NWORK,LCOUT,IER
C
      DOUBLE PRECISION A,AA,B,BB
      INTEGER I,I1,I2,KK,KORD,MODE,NB,NBSOUT,NBSPL,NDIM2,NDNEED,
     1        NDOM,NEED,NHOLD,NKTOT,NR,NRNG
      CHARACTER*51 MSG
      CHARACTER*8 SUBNAM
      DATA SUBNAM  / 'DTMGKT' /
C
C  CHECK INTEGER CONTROL VARIABLES OF INPUT SPLINE ARRAYS.
C
      IER = 0
      MODE = 1
      NDOM  = CIN(ICPTR(1))
      NRNG  = CIN(ICPTR(1)+1)
      KORD  = CIN(ICPTR(1)+2)
      NBSPL = CIN(ICPTR(1)+3)
      A     = CIN(ICPTR(1)+5)
      B     = CIN(ICPTR(1)+4+NBSPL+KORD)
      NEED = NBSPL
      IF (NBSPL .LT. KORD) IER = -6
      IF (KORD .LT. 1) IER = -1
      IF(NDOM .NE. 1) IER = -51
      IF (N .LT. 1) IER = -2
      IF (IER .LT. 0) GO TO 9900
      DO 100 I=2,N
        NDOM = CIN(ICPTR(I))
        NR   = CIN(ICPTR(I)+1)
        KK   = CIN(ICPTR(I)+2)
        NB   = CIN(ICPTR(I)+3)
        AA   = CIN(ICPTR(I)+5)
        BB   = CIN(ICPTR(I)+4+NB+KK)
        NEED = NEED + NB
        IF ((AA .NE. A) .OR. (BB .NE. B)) IER = -65
        IF (KK .NE. KORD) IER = -64
        IF (NR .NE. NRNG) IER = -52
        IF (NB .LT. KK) IER = -6
        IF(NDOM .NE. 1) IER = -51
  100 CONTINUE
      IF (NT .GT. 0) THEN
          IF(MLT(1) .GT. KORD) IER = -8
          NEED = NEED + MLT(1)
          DO 200 I=2,NT
              IF(T(I) .LE. T(I-1) .OR. MLT(I) .GT. KORD) IER = -8
              NEED = NEED + MLT(I)
  200     CONTINUE
          IF((T(1) .LT. A) .OR. (T(NT) .GT. B))  IER = -65
      END IF
      IF (IER .LT. 0) GO TO 9900
      NEED = NEED * (KORD+1) + N + NDIM
C
C     SET UP NEW KNOT ARRAY
C
      I1 = 1
      I2 = I1 + NDIM
      NDIM2 = (NDIM*N + 1)/2
      IF (NWORK .LT. NDIM+N) THEN
          IER = -3
          MODE = 2
          MSG ='THE FOLLOWING AMOUNT MAY BE EXCESSIVE'
          CALL DTERRX(MSG,WORK,0)
          MSG = 'HOWEVER, MORE WORK SPACE IS DEFINITELY NEEDED'
          CALL DTERRX(MSG,WORK,0)
          GO TO 9900
      END IF
      CALL DTMGK1(CIN,ICPTR,N,T,MLT,NT,COUT,COUT(NDIM2+1),
     1      WORK(I2),WORK(I1),NKTOT,NDIM,NDNEED)
      NBSOUT = NKTOT - KORD
      LCOUT  = 5 + KORD + (NRNG+1)*NBSOUT
      LCOUT = MAX(INT(NDNEED*2/N),LCOUT)
      IF (LCOUT .GT. NDIM) THEN
          IER = -61
          GO TO 9900
      END IF
      NEED = MAX(NBSOUT*(KORD+1), N) + NDIM
      IF (NEED .GT. NWORK) THEN
          IER = -3
          MODE = 2
          GO TO 9900
      END IF
C
C  *** FUTURE UPGRADE WILL CALL DTUPDG HERE IF INPUT CURVES
C  *** HAVE DIFFERENT POLYNOMIAL DEGREES.
C
C
C  LOOP THROUGH CURVES ADDING KNOTS
C
      NHOLD = NWORK - NDIM
      DO 750 I = 1,N
        CALL DTOSLO(CIN(ICPTR(I)),1,WORK,NKTOT,NDIM,
     1              WORK(I2),NHOLD,
     4              COUT(1+NDIM*(I-1)),IER)
        IF(IER .LT. 0) THEN
            MODE = 5
            IER = -100+IER
       END IF
  750 CONTINUE
C
C  NORMAL RETURN
C
 9900 IF (IER .LT. 0) THEN
          CALL DTERR(MODE,SUBNAM,IER,NEED)
          COUT(1) = -1.0D0
      END IF
      RETURN
C
      END


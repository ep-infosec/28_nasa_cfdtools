C<*>
      SUBROUTINE DTSPV2(X,KORD,TKTS,INCK,NKTS,LAST,NEXT,IER)
C
C  PURPOSE:  FIND SPAN IN WHICH X LIES FOR SPLINE EVALUATION.
C
C  INPUTS:
C           NKTS = NUMBER OF KNOTS, .GE. 2*KORD.
C           INCK = INCREMENT FOR KNOT ARRAY.
C           LAST = INITIAL GUESS.  IF .LE. KORD, KORD IS USED.
C                  IF .GE. NKTS - KORD, THAT IS USED INSTEAD.
C
C OUTPUT:   NEXT = TRUE INTERVAL IN WHICH X LIES.
C                  OR DTJCON(1) FOR ERROR RETURN.
C
C           IER  = SUCCESS/ERROR FLAG.
C                    0   NORMAL RETURN.
C                  -50   X OUT OF RANGE.
C
C  METHOD:  LINEAR SEARCH WITH MEMORY.
C
C  AUTHOR:  A.K. JONES
C
C  DATE:  24-SEP-84
C
C  MINOR REVISIONS BY FRITZ KLEIN, 21-JAN-85
C
C  BUG FIXED BY DEBBIE NELSON -- AUGUST, 1992.
C
      DOUBLE PRECISION X,TKTS(INCK,NKTS)
      EXTERNAL DTJCON
      INTEGER DTJCON
C
      IF(X .LT. TKTS(1,1) .OR. TKTS(1,NKTS) .LT. X) GO TO 9050
      NLAST = NKTS - KORD
      IN = MIN0(NLAST,MAX0(KORD,LAST))
      IF(X .LT. TKTS(1,IN)) GO TO 200
C
C  SEARCH FORWARD.
C
C  FIND IL SUCH THAT TKTS(1,IL) .LE. X .LT. TKTS(1,IL+1)
C  UNLESS X = TKTS(1,NLAST+1).  IN THIS CASE, FIND IL SUCH
C  THAT TKTS(1,IL) .LT. X .LE. TKTS(1,IL+1).
C
      DO 100 IL=IN,NLAST
        IF(X .LT. TKTS(1,IL+1)) GO TO 800
  100 CONTINUE
      IF(X .EQ. TKTS(1,NLAST+1)) THEN
        IL = NLAST
  150   CONTINUE
        IF ( TKTS(1,IL) .LT. TKTS(1,IL+1) ) GOTO 800
        IL = IL - 1
        GOTO 150
      ENDIF
      GO TO 9050
C
C  SEARCH BACKWARD.
C
C  HAND-CODED LOOP:  DO 300 IL=IN-1,KORD,-1
C
  200 CONTINUE
      IL = IN - 1
  300 CONTINUE
        IF(X .GE. TKTS(1,IL)) GO TO 800
        IL = IL - 1
      IF(IL .GE. KORD) GO TO 300
      GO TO 9050
C
C  NORMAL RETURN
C
  800 CONTINUE
      IER = 0
      NEXT = IL
      RETURN
C
C  ERROR RETURNS
C
 9050 CONTINUE
      IER = -50
      NEXT = DTJCON(1)
      RETURN
      END

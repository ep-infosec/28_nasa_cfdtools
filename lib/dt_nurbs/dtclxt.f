C***** DTCLXT *****
C   CURVE AND LINE INTERSECTION IN THE PLANE
C
C   INPUT:
C     C       SPLINE DATA ARRAY FOR A PLANAR CURVE.
C     XLINE   ARRAY OF COEFFICIENTS FOR AN EQUATION OF THE LINE:
C             XLINE(1)*X + XLINE(2)*Y = XLINE(3)
C   WORKSPACE:
C     WORK    WORK ARRAY OF LENGTH NWORK.
C     NWORK   THE LENGTH OF THE ARRAY WORK.  NWORK >= 3 * MC + 100,
C             WHERE  MC = 5 + C(3) + 2*C(4)
C   OUTPUT:
C     NPTS    THE NUMBER OF ISOLATED INTERSECTION POINTS.
C     PTS     THE ARRAY WHICH CONTAINS THE PARAMETER VALUES OF THE 
C             INTERSECTION POINTS.
C     NINTS   THE NUMBER OF INTERVALS OVER WHICH THE SPLINE AND LINE 
C             ARE IDENTICAL.
C     PINTS   THIS ARRAY CONTAINS THE PARAMETER INTERVALS OVER WHICH 
C             THE SPLINE AND LINE ARE IDENTICAL.  THE ITH INTERVAL
C             STARTS AT PINTS(1,I) AND RUNS TO PINTS(2,I), FOR EACH
C             I=1,...,NINTS.  IF THE SPLINE IS DISCONTINUOUS, THEN THE
C             INTERVALS SPECIFIED MAY BE CLOSED ON THE LEFT AND OPEN ON THE
C             RIGHT.
C     IER     SUCCESS/ERROR CODE.  FOR IER < 0, RESULTS HAVE NOT BEEN
C             COMPUTED.
C
C   CALLS:
C     DTERR
C     DTGET
C     DTSZER
C*****
      SUBROUTINE DTCLXT (C, XLINE, WORK, NWORK, NPTS, PTS, NINTS, 
     +    PINTS, IER)
      INTEGER NWORK, NPTS, NINTS, IER
      DOUBLE PRECISION C(*), XLINE(3), WORK(NWORK), PTS(*), PINTS(2,*)
C
C   LOCAL VARIABLES
      INTEGER N, MRAW, M, KORD, NCOEF, NEED, MC
      DOUBLE PRECISION DUM
      CHARACTER*8 SUBNAM
C
      DATA SUBNAM /'DTCLXT'/
C
C   CHECK INPUTS AND GET SPLINE PARAMETERS
      IF (XLINE(1) .EQ. 0.0D0 .AND. XLINE(2) .EQ. 0.0D0) GO TO 9007
      CALL DTGET (C, .TRUE., 1, N, MRAW, M, KORD, NCOEF, DUM, DUM, IER) 
      IF (IER .NE. 0) GO TO 9900
      IF (N .NE. 1) GO TO 9001
      IF (M .NE. 2) GO TO 9002
      MC = 5 + KORD + 2 * NCOEF
      NEED = 3 * MC + 100
      IF (NEED .GT. NWORK) GO TO 9006
C
C   BUILD REAL-VALUED SPLINE FUNCTION
C     COPY PARAMETERS AND KNOT VECTOR
      DO 10 I=1,5+KORD+NCOEF
        WORK(I) = C(I)
  10  CONTINUE
C     CHANGE TO REAL-VALUED (I.E. ONE DEPENDENT VARIABLE)
      WORK(2) = 1.0D0
C     CONSTRUCT COEFFICIENTS
      DO 20 I=6+KORD+NCOEF,MC
        WORK(I) = XLINE(1)*C(I) + XLINE(2)*C(I+NCOEF)
        IF (MRAW .LT. 0) THEN
C         FROM RATIONAL SPLINE
          WORK(I) = WORK(I) - XLINE(3)*C(I+2*NCOEF)
        ELSE
C         FROM POLYNOMIAL SPLINE
          WORK(I) = WORK(I) - XLINE(3)
        END IF
  20  CONTINUE
C
C   CALL DTSZER TO FIND ZEROES OF REAL-VALUED SPLINE FUNCTION
      CALL DTSZER (WORK, WORK(MC+1), NWORK-MC, NPTS, PTS, NINTS, 
     +    PINTS, IER)
      IF (IER .LT. 0) THEN
C       REINTERPRET DTSZER ERROR CODES
        IF (IER .EQ. -3) GO TO 9006
        GO TO 9099
      END IF
C   NORMAL EXIT
      RETURN
C
C   ERROR EXITS
C   NOT A CURVE
 9001 IER = -1
      GO TO 9900
C   NOT PLANAR
 9002 IER = -2
      GO TO 9900
C   INSUFFICIENT WORKSPACE
 9006 IER = -6
      CALL DTERR (2, SUBNAM, IER, NEED)
      RETURN
C   BAD LINE COEFFICIENTS
 9007 IER = -7
      GO TO 9900
C   UNEXPECTED ERROR FROM DTSZER
 9099 IER = -99
      CALL DTERR (4, SUBNAM, IER, 0)
      RETURN
C   NORMAL ERROR MESSAGE
 9900 CONTINUE
      CALL DTERR (1, SUBNAM, IER, 0)
      RETURN
      END

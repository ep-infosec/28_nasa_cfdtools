      SUBROUTINE DTUPD1 ( NRNG, KORDIN,NDK, NT, T, CPTIN,INC1,NDIM1,
     1                    CC, CW, NU, U, CPTOUT, INC2,
     2                    NDIM2)
C
      INTEGER    NRNG, KORDIN, NT, INC1, NDIM1, NU,
     1           INC2, NDIM2
      INTEGER    IRNG, NUX, I, J, II, LBEGIN, LEND, L, ISTOP, K
C
      DOUBLE PRECISION    T(*), U(*), CPTIN(INC1,NDIM1,*),
     1           CPTOUT(INC2,NDIM2,*)
C
      DOUBLE PRECISION    CC (*), CW (*)
      DOUBLE PRECISION    YI,YIK,Y1,Y2,DEN
C
C=====
C
C
C     PURPOSE:  RAISE THE DEGREE OF A SPLINE BY ONE
C
C
C     METHOD:   COHEN, LYCHE, AND SHUMAKER ALGORITH
C
C     AUTHOR:   MARY H. VOYTKO
C
C     PARAMETERS:
C
C     INPUTS:
C
C             NRNG     INTEGER REFERING TO THE DEPENDENT VARIABLE. THIS
C                      IS USUALLY SET TO 1.
C             KORDIN   INTEGER ORDER OF THE INPUT SPLINE.
C             NDK      INTEGER NUMBER OF DISTINCT KNOTS.
C             NT       INTEGER NUMBER OF COEFFICIENTS IN THE INPUT VECTOR.
C             T        DOUBLE PRECISION ARRAY OF LENGTH NT THAT CONTAINS
C                      THE COEFFICIENTS.
C             CPTIN    DOUBLE PRECISION ARRAY THAT CONTAINS THE
C                      ORIGINAL KNOT SET.
C                      IT IS A 3D ARRAY DIMENSIONED (INC1,NDIM1,NRNG).
C             INC1     INTEGER STRIDE THROUGH THE CPTIN ARRAY
C             NDIM1    INTEGER NUMBER OF KNOTS IN THE DIRECTION OF THE
C                      INDEPENDENT VARIABLE.
C
C      WORK ARRAYS:
C
C             CC       DOUBLE PRECISION WORK ARRAY OF LENGTH KORDIN.
C             CW       DOUBLE PRECISION WORK ARRAY OF LENGTH KORDIN.
C
C      OUTPUTS:
C
C             KOUT  INTEGER ORDER OF THE OUTPUT SPLINE.
C             NU       INTEGER NUMBER OF NEW COEFFICIENTS.
C             U        DOUBLE PRECISION ARRAY OFLENGTH NU OF
C                      CALCULATED COEFFICIENTS.
C             CPTOUT   DOUBLE PRECISION ARRAY CONTAINING THE REFINED
C                      KNOT SEQUENCE.
C                      IT IS OF LENGTH INC2*NDIN2*NRNG.
C             INC2     INTEGER STRIDE THROUGH THE KNOT VECTOR.
C             NDIM2    INTEGER NUMBER OF KNOTS IN THE REFINED KNOT SEQUENCE.
C=====
C
C
C THE COEFFICIENTS, KNOTS, AND CONTROL INFORMATION HAS ALREADY
C BEEN PUT IN SEPARATE ARRAYS. THE ALGORITHM UPS THE
C DEGREE AS FOLLOWS:
C  1: FOR ALL KNOTS IN THE NEW KNOT SET, LOCATE THE KNOT
C     RELATIVE TO THE NEW KNOT SET
C
C
C**** COMPUTE THE NUMBER OF NEW COEFFICIENTS TO BE CALCULATED
C
C
      DO 900 IRNG = 1, NRNG
C
      NUX = NDIM1 + NDK - 1
C
C
      DO 100 J = 1, NUX
         I = 1
 150  IF ( U(J) .EQ. T(I) .AND. U(J) .LT. T(I+1) ) GOTO 200
         I = I + 1
         IF ( I .EQ. NU ) GOTO 200
         GOTO 150
C
 200  CONTINUE
C
C        2: INITIALIZE THE WORK ARRAYS FOR THIS PASS THROUGH
C
         DO 250 II = 1, KORDIN
            CC(II) = 0.0
            CW(II) = 0.0
 250     CONTINUE
C
         LEND   = MIN ( KORDIN, NDIM1+KORDIN-I )
         LBEGIN = MAX ( 1,KORDIN+1-I )
         DO 270 L = LBEGIN,LEND
             ISTOP = L + I - KORDIN
             CC(L) = CPTIN(1,ISTOP,IRNG)
             CW(L) = CC(L)
  270    CONTINUE
C
C
 300     CONTINUE
C
C        3: CALCULATE THE NEW COEFFICIENTS
C
         DO 400 K = KORDIN-1, 1, -1
C
            Y1 = U(J+K)
            Y2 = U(J+K+1)
            LBEGIN = MIN(KORDIN,NT+2*KORDIN-K-I)
            LEND   = MAX(KORDIN-K+1,1-I+KORDIN)
C
C
            DO 500 L = LBEGIN, LEND, -1
               YI  = T(I + L - KORDIN)
               YIK = T(I+L+K-KORDIN)
               DEN = YIK - YI
C
               CC(L) = ((Y2-YI)*CC(L) + (YIK -Y2)*CC(L-1)) / DEN
               CW(L) = ((Y1-YI)*CW(L) + (YIK -Y1)*CW(L-1)) / DEN + CC(L)
C
 500        CONTINUE
 400     CONTINUE
C
         CPTOUT(1,J,IRNG) = CW(KORDIN) / KORDIN
C
 100   CONTINUE
C
 900  CONTINUE
C
      RETURN
C
      END


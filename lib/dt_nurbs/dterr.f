      SUBROUTINE DTERR(MODE,SUBNAM,IER,N)
C
C**** STANDARD ERROR HANDLER
C
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C *                                                     *
C *   WARNING - IF OVERLAYS ARE USED, THIS SUBROUTINE   *
C *             AND THE COMMON BLOCK /DTERRC/ MUST BE   *
C *             IN THE ROOT SEGMENT.                    *
C *                                                     *
C * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C
      EXTERNAL DTJCON
      INTEGER DTJCON
C
      INTEGER IEUNIT, IEFLAG, NERRS, IPON, IXPAND, INITL, IFLAG, NONO
C
      COMMON/DTERRC/IEUNIT,IEFLAG,NERRS,IPON,IXPAND(2)
      CHARACTER*(*) SUBNAM
C
      SAVE
      DATA INITL,IFLAG,NONO/0,10,5/
C
C
C INPUT VARIABLES
C
C     MODE   - MODE CONTROL PARAMETER FOR THE ERROR HANDLER
C
C     SUBNAM - NAME OF CALLING ROUTINE IN  CHARACTER*(*) FORM
C              (IGNORED IF MODE.LT.0)
C
C     IER    - ERROR CODE FROM CALLING ROUTINE
C              (IGNORED IF MODE.LT.0)
C
C     N      - NUMBER OF WORDS OF WORKING
C                   STORAGE REQUIRED IF MODE.EQ.2
C              VALUE TO BE PLACED IN IEUNIT IF MODE.EQ.-2
C              VALUE TO BE PLACED IN IEFLAG IF MODE.EQ.-3
C              (IGNORED OTHERWISE)
C
C
C INPUT/OUTPUT COMMON VARIABLES
C
C     IEUNIT - WRITE UNIT FOR ERROR MESSAGES
C
C     IEFLAG - IF .GT.0, OK TO PRINT AN ERROR MESSAGE
C              IF .EQ.0, PRINT TRACEBACK OR EXTRA MESSAGES ONLY
C              IF .LT.0, NO PRINT PERMITTED
C
C     NERRS  - CONTAINS TOTAL NUMBER OF ERRORS (PRINTED OR OTHERWISE)
C
C     IXPAND - UNUSED SPACE FOR FUTURE EXPANSION
C
C
C OUTPUT VARIABLES
C
C     NONE
C
C
C SIGNIFICANT LOCAL VARIABLES
C
C     INITL  - SAVE VARIABLE THAT CONTROLS BYPASS OF INITIALIZATION
C
C     IFLAG  - DEFAULT VALUE OF MAX MESSAGES TO BE PRINTED
C
C     NONO   - SAVE VARIABLE THAT CONTROLS PRINTING OF MESSAGES WHEN
C              THE ERROR HANDLER IS ABUSED  (SUCH MESSAGES ARE
C              PRINTED EVEN WHEN IEFLAG.LT.0)
C
C
C USAGE OF DTERR
C
C     CHARACTER*(*) SUBNAM
C     CALL DTERR(MODE,SUBNAM,IER,N)
C
C
C
C     MODE = -6  -  SET DTERR TO PREVIOUS ERROR COUNTING AND MESSAGE
C                   PRINTING CONDITION (UNLESS ERROR COUNTING AND
C                   MESSAGE PRINTING ARE ALREADY TURNED ON)
C
C          = -5  -  SET DTERR TO STOP COUNTING ERRORS AND TO DO NO
C                   MESSAGE PRINTING
C
C          = -4  -  RESTORE DEFAULT VALUE OF IEUNIT
C
C          = -3  -  SET IEFLAG = N (THIS IS A COUNTDOWN VARIABLE
C                   FOR MAX MESSAGES)
C
C          = -2  -  SET IEUNIT = N (VALIDITY NOT CHECKED HERE)
C
C          = -1  -  FORCE COMMON BLOCK INITIALIZATION IF NOT YET SET
C
C          =  0  -  PRINT WARNING MESSAGE USING SUBNAM,IER
C
C          =  1  -  PRINT INPUT ERROR MESSAGE USING SUBNAM,IER
C
C          =  2  -  PRINT WORKING STORAGE MESSAGE USING SUBNAM,IER,N
C
C          =  3  -  PRINT PROCESS ERROR MESSAGE USING SUBNAM,IER
C
C          =  4  -  PRINT TRACEBACK USING SUBNAM,IER
C
C          =  5  -  PRINT UNEXPECTED TRACEBACK USING SUBNAM,IER
C
C
C SUBROUTINES CALLED
C
C     FUNCTION DTJCON
C
C
C PROGRAMMED BY
C
C     STUART L. ANDERSON - 2 OCT. 1979
C
C REVISED BY STUART L. ANDERSON - 29 JULY 1980
C
C     MODIFIED FOR FTN5 BY V. SINGSAAS/G. EDGAR
C         11/5/85.
C
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * START OF CODE
C
C**** INITIALIZE ON FIRST ENTRY.
C
      IF(INITL.NE.0)GO TO 10
          INITL  = 1
          IEUNIT = DTJCON(4)
          IEFLAG = IFLAG
          NERRS  = 0
          IPON = 0
   10 CONTINUE
C
C**** IF DTERR HAS BEEN USED IMPROPERLY, TAKE ERROR EXIT.
C
      IF(MODE.GE.(-6) .AND. MODE.LE.5)GO TO 30
          IF(NONO.LE.0)RETURN
          NONO = NONO-1
          WRITE(IEUNIT,20)MODE,SUBNAM
   20     FORMAT(44H0***** ERROR HANDLER (DTERR) USED IMPROPERLY /
     +           7X, 6HMODE = ,I8, 6H FROM , A/)
          RETURN
   30 CONTINUE
C
C**** IF MODE.LT.0, SET PARAMETER AND RETURN.
C
      IF(MODE.GE.0)GO TO 40
          IF(MODE.EQ.(-1))RETURN
          IF(MODE.EQ.(-2))IEUNIT = N
          IF(MODE.EQ.(-3))IEFLAG = N
          IF(MODE.EQ.(-4))IEUNIT = DTJCON(4)
          IF(MODE.EQ.-5) THEN
          IPON=IPON+1
          RETURN
            ENDIF
          IF(MODE.EQ.-6) THEN
          IF(IPON.GT.0)IPON=IPON-1
             RETURN
            ENDIF
          RETURN
   40 CONTINUE
C
C
C IF   (MODE == NOPRINT, NOCOUNT) DO NOTHING ELSE, AND RETURN
C
      IF(IPON.GT.0) RETURN
C
C
C**** IF TRACEBACK, BYPASS ERROR COUNT PROCESSING.
C
CCCCCC      IF(MODE.GE.4)GO TO 200
C
C****     ISSUE MESSAGE LIMIT COMMENT IF MESSAGES HAVE BEEN
C****     ON AND THIS CALL IS THE FIRST TO BE IGNORED.
C
          IF(IEFLAG.NE.0 .OR. NERRS.EQ.0)GO TO 110
          IF (MODE .GE. 4) GO TO 110
              WRITE(IEUNIT,100)
  100         FORMAT(42H0***** ERROR HANDLER (DTERR) MESSAGE LIMIT ,
     +                9H EXCEEDED /
     +                7X,31HONE OR MORE MESSAGES SUPPRESSED /)
  110     CONTINUE
C
C****     UPDATE COUNTERS.  IF PRINTING NOT ALLOWED, RETURN.
C
      IF(MODE.GE.4) THEN
          IF (IEFLAG .LT. 0) RETURN
      ELSE
          NERRS  = NERRS+1
          IEFLAG = MAX0(IEFLAG-1,-2)
          IF(IEFLAG.LT.0)RETURN
      END IF
C
C**** END OF BYPASS.
C
  200 CONTINUE
C
C**** SELECT PRIMARY MESSAGE.
C
      IF(MODE.GT.0)GO TO 310
          WRITE(IEUNIT,300)SUBNAM
  300     FORMAT(38H0***** WARNING REPORTED BY SUBROUTINE ,A)
          GO TO 370
C
  310 CONTINUE
      IF(MODE.GT.2)GO TO 330
          WRITE(IEUNIT,320)SUBNAM
  320     FORMAT(39H0***** INPUT ARGUMENT ERROR REPORTED BY ,
     +           12H SUBROUTINE ,A)
          GO TO 370
C
  330 CONTINUE
      IF(MODE.GT.3)GO TO 350
          WRITE(IEUNIT,340)SUBNAM
  340     FORMAT(44H0***** PROCESS ERROR REPORTED BY SUBROUTINE ,A)
          GO TO 370
C
  350 CONTINUE
          WRITE(IEUNIT,360)SUBNAM
  360     FORMAT(34H ***** ABOVE CALLED BY SUBROUTINE ,A)
C
  370 CONTINUE
C
C**** WRITE SECONDARY MESSAGE(S).
C
      IF(MODE.NE.2)GO TO 510
          WRITE(IEUNIT,500)N
  500     FORMAT(3X,I8,32H WORDS OF WORKING STORAGE NEEDED )
  510 CONTINUE
C
      IF(MODE.NE.5)GO TO 530
          WRITE(IEUNIT,520)
  520     FORMAT(7X,20HERROR WAS UNEXPECTED)
  530 CONTINUE
C
      WRITE(IEUNIT,540)SUBNAM,IER
  540 FORMAT(7X,5HSEE  ,A,16H ABSTRACT (IER = ,I7,1H) /)
C
C**** ALL DONE, RETURN
C
      RETURN
      END

      SUBROUTINE DTOSLO(CIN,IDOM,TKTS,NKTS,NCOUT,WORK,
     1                  NWORK,COUT,IER)
C+
C  PURPOSE: ADD KNOTS TO THE REPRESENTATION OF A SPLINE FUNCTION.
C
C  INPUTS:  CIN   INPUT SPLINE, IN C VECTOR FORM.
C           IDOM  INDEX OF THE INDEPENDENT VARIABLE IN WHICH
C                 THE KNOT SET IS TO BE REPLACED.
C           TKTS  NEW KNOT SET.  MUST CONTAIN ALL EXISTING KNOTS,
C                 WITH MULTIPLICITIES AT LEAST AS LARGE AS
C                 THOSE IN THE EXISTING KNOT SET.
C           NKTS  NUMBER OF KNOTS IN TKTS, INCLUDING ALL MULTIPLICITIES.
C           NCOUT LENGTH OF COUT ARRAY PASSED IN.
C                 UNIVARIATE SPLINE, CIN(1) = 1, THE FOLLOWING
C                 COMPUTATION CAN BE USED:
C                   NCOUT = 5 + CIN(3) + (CIN(2)+1)*(NKTS-CIN(3)).
C                 FOR THE GENERAL CASE, LET
C                   NDOM  = CIN(1)
C                   NRNG  = CIN(2)
C                   K(J)  = CIN(2+J)
C                   N(J)  = CIN(2+NDOM+J)
C                   NK(J) = N(J) + K(J)
C                   NCP   = AS DEFINED BELOW UNDER WORKING STORAGE.
C                 THEN THE FOLLOWING COMPUTATION CAN BE USED.
C                   NCOUT = 2 + 3*NDOM
C                    + NK(1)+NK(2)+..+NK(IDOM-1) + NKTS
C                    + NK(IDOM+1)+..+NK(NDOM)
C                    +  NRNG *
C                       N(1)*N(2)*..*N(IDOM-1)*NCP*N(IDOM+1)..*N(NDOM)
C
C  WORKING STORAGE:
C
C           WORK  WORK ARRAY OF LENGTH NWORK.
C           NWORK LENGTH OF WORK ARRAY.
C                 IF KORD = CIN(2+IDOM) = ORDER OF SPLINE IN VARIBALE NO. IDOM
C                    NCP  = NKTS - KORD = NUMBER OF OUTPUT
C                                 COEFFICIENTS IN VARIABLE NO. IDOM,
C                 THEN NWORK .GE. (KORD+1) * NCP.
C
C  OUTPUT:  COUT  OUTPUT SPLINE, IN C VECTOR FORM.
C
C           IER   SUCCESS/ERROR CODE.
C                 IER =   0  SUCCESS, RESULTS COMPUTED.
C                 IER =  -1  ORDER OF INPUT SPLINE .LT. 1.
C                 IER =  -3  NWORK TOO SMALL.
C                 IER =  -6  INPUT SPLINE HAS TOO FEW COEFFICIENTS.
C                 IER =  -8  KNOTS NOT INCREASING, OR MULTIPLICITIES TOO HIGH,
C                            EITHER IN INPUT SPLINE OR IN TKTS ARRAY.
C                 IER =  -51 NUMBER OF INDEPENDENT VARIABLES IN INPUT SPLINE
C                            .LT. 1
C                 IER =  -52 NUMBER OF DEPENDENT VARIABLES IN INPUT SPLINE
C                            .LT. 1.
C                 IER =  -61 NCOUT TOO SMALL.
C                 IER =  -62 IDOM .LT. 1, OR GREATER THAN NUMBER OF DEPENDENT
C                            VARIABLES IN INPUT SPLINE.
C                 IER =  -63 NKTS .LT. NUMBER OF KNOTS IN INPUT SPLINE.
C                 IER = -100 UNEXPECTED ERROR IN A LOWER-LEVEL ROUTINE.
C
C  DATE:  5-DEC-84
C        20-JAN-86 ADD SUPPORT FOR IDOM .GT. 1
C-
      DOUBLE PRECISION CIN(*),TKTS(*),WORK(*),COUT(*)
      DOUBLE PRECISION DTMCON
      INTEGER IDOM,NKTS,NWORK,NCOUT,IER
      CHARACTER*8 SUBNAM
      DATA SUBNAM / 'DTOSLO' /
C
C  ERROR-CHECK INPUTS AND CONTROL PARAMETERS FROM CIN.
C
      NDOM  = CIN(1)
      NRNG  = CIN(2)
C
C     IF THE SPLINE IS RATIONAL, TREAT THE DENOMINATOR AS ONE
C     MORE DEPENDENT VARIABLE...
C
      IF (NRNG .LT. 0) THEN
          NRNG = -NRNG
      ENDIF
C
      IF(NDOM .LT. 1) THEN
        IER = -51
        GO TO 9100
      ELSE IF(NRNG .LT. 1) THEN
        IER = -52
        GO TO 9100
      ELSE IF(IDOM .LT. 1 .OR. IDOM .GT. NDOM) THEN
        IER = -62
        GO TO 9100
      END IF
      KORD   = CIN(2+IDOM)
      NCPIN  = CIN(2+NDOM+IDOM)
      NKTIN  = NCPIN + KORD
      NCPOUT = NKTS - KORD
      NEED   = (KORD+1) * NCPOUT
      IF(KORD .LT. 1) THEN
        IER = -1
        GO TO 9100
      ELSE IF(NEED .GT. NWORK) THEN
        IER = -3
        GO TO 9200
      ELSE IF(NCPIN .LT. KORD) THEN
        IER = -6
        GO TO 9100
      ELSE IF(NKTS .LT. NKTIN) THEN
        IER = -63
        GO TO 9100
      END IF
C
C  LOOP THROUGH INDEPENDENT VARIABLES .LT. IDOM, TO FIND
C  IKPTR = POINTER TO KNOT SET NO. IDOM IN CIN AND COUT,
C  AND NCPLO = PRODUCT(NCPJ: J .LT. IDOM).
C
      IKPTR = 3*NDOM + 3
      NCPLO = 1
      DO 100 J=1,IDOM-1
        KORDJ = CIN(2+J)
        NCPJ  = CIN(2+NDOM+J)
        NCPLO = NCPLO * NCPJ
        IKPTR = IKPTR + NCPJ + KORDJ
  100 CONTINUE
C
C  CHECK KNOTS IN IDOM DIRECTION INCREASING AND MULTIPLICITES NOT TOO HIGH.
C
      DO 110 I=1,NKTIN-1
        IF(CIN(IKPTR-1 + I) .GT. CIN(IKPTR + I)) THEN
          IER = -8
          GO TO 9100
        END IF
  110 CONTINUE
      DO 120 I=1,NKTIN-KORD
        IF(CIN(IKPTR-1 + I) .GE. CIN(IKPTR+KORD-1 + I)) THEN
          IER = -8
          GO TO 9100
        END IF
  120 CONTINUE
C
C  DO SAME FOR USER-INPUT KNOT SET TKTS(*).
C
      DO 130 I=1,NKTS-1
        IF(TKTS(I) .GT. TKTS(I+1)) THEN
          IER = -8
          GO TO 9100
        END IF
  130 CONTINUE
      DO 140 I=1,NKTS-KORD
        IF(TKTS(I) .GE. TKTS(I+KORD)) THEN
          IER = -8
          GO TO 9100
        END IF
  140 CONTINUE
C
C  LOOP THROUGH INDEPENDENT VARIABLES .GT. IDOM,
C  COUNTING UP TOTAL NUMBER OF KNOTS
C  AND NCPHI = PRODUCT(NCPJ: J .GT. IDOM).
C
      NCPHI  = 1
      ICPTR  = IKPTR + NKTIN
      DO 200 J=IDOM+1,NDOM
        KORDJ  = CIN(2+J)
        NCPJ   = CIN(2+NDOM+J)
        NCPHI  = NCPHI * NCPJ
        ICPTR  = ICPTR + NCPJ + KORDJ
  200 CONTINUE
      ICPTR2 = ICPTR + (NKTS - NKTIN)
C
C  NOW ICPTR  POINTS TO COEFFICIENTS IN CIN
C      ICPTR2 POINTS TO COEFFICIENTS IN COUT
C      NCPLO*NCPHI*NCPIN(NCPOUT) = TOTAL NUMBER OF COEFFICIENTS
C                                  PER DEPENDENT VARIABLE IN CIN(COUT)..
C
      NCNEED = (ICPTR2 - 1) + NRNG * NCPLO*NCPHI*NCPOUT
      IF(NCOUT .LT. NCNEED) THEN
        IER = -61
        GO TO 9100
      END IF
C
C  COPY CONTROL DATA AND KNOTS INTO CNEW
C
      DO 300 I=1,IKPTR-1
        COUT(I) = CIN(I)
  300 CONTINUE
      COUT(2+NDOM+IDOM) = NCPOUT
      DO 400 I=1,NKTS
        COUT(IKPTR+I-1) = TKTS(I)
  400 CONTINUE
      DO 500 I=IKPTR+NKTIN,ICPTR-1
        COUT(I+(NKTS-NKTIN)) = CIN(I)
  500 CONTINUE
C
C  CALL DTOSL1 ITERATIVELY TO GET NEW COEFFICIENTS.
C
      INC1  = NCPLO
      INC2  = NCPLO
      NDIM1 = NCPIN
      NDIM2 = NCPOUT
      IW    = NCPOUT * KORD + 1
      ICC   = -1
      DO 600 J=1,NCPLO
        CALL DTOSL1(NRNG*NCPHI,KORD,NKTIN,CIN(IKPTR),NKTS,TKTS,
     1              CIN(ICPTR-1+J),  INC1,NDIM1,ICC,WORK,WORK(IW),
     2              COUT(ICPTR2-1+J),INC2,NDIM2,IER)
        IF(IER .NE. 0) THEN
          IER = -100
          GO TO 9300
        END IF
  600 CONTINUE
C
C  NORMAL RETURN.
C
      IER = 0
      RETURN
C
C  ERROR RETURNS.
C
 9100 CONTINUE
      MODE = 1
      GO TO 9900
 9200 CONTINUE
      MODE = 2
      GO TO 9900
 9300 CONTINUE
      MODE = 3
 9900 CONTINUE
      COUT(1) = -1.0D0
      CALL DTERR(MODE,SUBNAM,IER,NEED)
      RETURN
C
      END

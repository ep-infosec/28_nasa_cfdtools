      SUBROUTINE DTRPST(INIT)
C
C     DTRPST IS A PORTABLE INITIALIZATION ROUTINE FOR THE PORTABLE
C     UNIFORM RANDOM NUMBER GENERATOR DTRPUN.  (PORTABILITY ASSUMES
C     AT LEAST 16 BIT INTEGER ARITHMETIC.)
C
C PURPOSE
C
C     THE UNIFORM RANDOM NUMBER GENERATOR DTRPUN MUST BE INITIALIZED.
C     FOR MANY APPLICATIONS, ZERO IS A CONVENIENT INITIAL VALUE.
C     HOWEVER, IF A DIFFERENT SEQUENCE OF RANDOM NUMBERS IS DESIRED
C     ON A LATER RUN, A DIFFERENT INITIAL VALUE MUST BE USED.
C
C USAGE
C
C     DTRPST MUST BE CALLED ONCE BEFORE ANY USE OF DTRPUN.  NOTE THAT
C     SUBROUTINES DTRPNR (NORMAL DISTRIBUTION) AND DTRPEX
C     (EXPONENTIAL DISTRIBUTION) ALSO USE DTRPUN.
C
C         CALL DTRPST(INIT)
C
C INPUT
C
C     INIT IS AN INTEGER VALUE IN THE RANGE 0.LE.INIT.LE.32767.  IF
C     INIT IS NOT IN THIS RANGE, IT IS INTERNALLY REPLACED BY
C     MOD(IABS(INIT),32768).
C
C OUTPUT
C
C     ALL OUTPUT IS INTO THE INTERNAL COMMUNICATION COMMON BLOCK
C     DTRPCM.  THESE ITEMS WILL RECEIVE THEIR INITIAL VALUES.
C
C     COMMON/DTRPCM/TWOPI,XNORM,INORM,ISEED(5)
C     SAVE /DTRPCM/
C
C         TWOPI     2.*PI
C
C         XNORM     HOLDING AREA FOR A SURPLUS NORMAL VARIABLE
C
C         INORM     FLAG FOR XNORM (XNORM CONTAINS A VALID NUMBER
C                   IF AND ONLY IF INORM.EQ.0.)
C
C         ISEED     THE DTRPUN SEED IN BASE 2**7
C
C SUBROUTINES CALLED
C
C     DTMCON
C
C PROGRAMMED BY
C
C     STUART L. ANDERSON - 28 MAR. 1979
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
      REAL TWOPI, XNORM
      INTEGER INORM, ISEED
      COMMON/DTRPCM/TWOPI,XNORM,INORM,ISEED(5)
      SAVE /DTRPCM/
C
      DOUBLE PRECISION DTMCON
      INTEGER KNIT
C
C**** GET ABSOLUTE VALUE OF INIT.  THE SECOND IF TEST IS MEANINGFUL
C**** ON TWO-S COMPLEMENT COMPUTERS.
C
      KNIT = INIT
      IF(KNIT.LT.0)KNIT = -KNIT
      IF(KNIT.LT.0)KNIT = 0
C
C**** MOVE KNIT (MOD 2**15) TO ISEED (BASE 2**7)
C
      ISEED(5) = MOD(KNIT,128)
      KNIT     = KNIT/128
      ISEED(4) = MOD(KNIT,128)
      KNIT     = KNIT/128
      ISEED(3) = MOD(KNIT,2)
      ISEED(2) = 0
      ISEED(1) = 0
C
C**** INITIALIZE REST OF COMMON
C
      TWOPI = 2.*DTMCON(12)
      XNORM = 0.
      INORM = 1
      RETURN
      END

*DECK DTWRAG
CDTWRAG
C**********************************************************************
      SUBROUTINE DTWRAG (CARY,ICARY,JUN,AGPSF,IERR)
C***********************************************************************
C
C FUNCTION-
C          CONVERT DTRC FORMAT (BCSLIB) B-SPLINE DATA ARRAY TO
C          TO A FILE TO BE READ IN BY AGPS "RBS" ROUTINE.
C
C AUTHORS-
C          C.P.CHI             CREATION  JUL 25 1989
C          P.KRAUSHAR          MODIFIED  JAN 12 1991
C
C INPUT-
C         CARY   = C-ARRAY CONTAINING DTRC FORMAT B-SPLINE DATA
C         ICARY  = DIMENSION OF C-ARRAY
C         JUN    = UNIT FOR OUTPUT FILE
C         AGPSF  = FILE NAME FOR OUTPUT FILE IN AGPS FORMAT
C OUTPUT-
C         IERR   = ERROR FLAG
C                = 0   NO ERROR DETECTED
C                = -1  MORE THAN 2 INDEPENDENT VARIABLE
C                = -2  INCORRECT NUMBER OF DIMENSION
C                      SECOND ELEMENT OF C-ARRAY GREATER THAN 3 OR
C                      LESS THAN -4
C                = -3  NUMBER OF DATA IS GREATER THAN THE SPECIFIED
C                      SIZE OF THE C-ARRAY
C                = -4  WRITE ERROR
C
C REFERENCES-
C          1.
C
C NOTES-
C
C TYPE/PARAMETER/DIMENSION/COMMON/EQUIVALENCE/DATA/FORMAT STATEMENTS-
C
      PARAMETER (ICOEF=3)
C
      CHARACTER*(*) AGPSF
      CHARACTER CFMT*1,FMT*18
C
      DOUBLE PRECISION PARRAY(ICOEF)
      DOUBLE PRECISION CARY(ICARY)
      DOUBLE PRECISION VST, VEN, DENOM
C
      LOGICAL RATNL
C
      DATA PARRAY /ICOEF*0.0/
C
   20 FORMAT(8I5)
   30 FORMAT(3I4,I5)
   40 FORMAT(I3/(5F12.6))
C
C ********************** START OF EXECUTABLE CODE **********************
C
      IERR=0
C
C...OPEN AGPS FILE (OUTPUT FROM THIS ROUTINE)
C
      OPEN(UNIT=JUN,FILE=AGPSF,FORM='FORMATTED',STATUS='UNKNOWN')
C
      NOBJ=0
      NLNS=0
      NOBJ=NOBJ+1
C
C...CONVERT DTRC FORMAT TO AGPS DATA
C
C...CONVERT GENERAL DATA
C
C...NO OF INDEPENDENT VARIABLES
C
      NPV =INT(CARY(1))
C
      IF(NPV.LT.1.OR.NPV.GT.2) THEN
        IERR=-1
        GOTO 999
      ENDIF
C
C...NO OF DEPENDENT VARIABLE AND PHYSICAL DIMENSION
C
      IDIM = INT(CARY(2))
      RATNL = (IDIM .LT. 0)
      IF (RATNL) THEN
        IDIM = - IDIM
        IPHY = IDIM - 1
      ELSE
        IPHY = IDIM
        DENOM=1.0
      ENDIF
      IF (IPHY.EQ.0 .OR. IPHY.GT.ICOEF) THEN
        IERR=-2
        GOTO 999
      ENDIF
C
C     NDIM=IPHY
CTEMP SOME VERSIONS OF AGPS ONLY ACCEPT 3D AT THIS TIME
      NDIM=3
C
C...DEFINE FORMAT FOR WRITE CONTROL POINT DATA
C
      WRITE(CFMT,'(I1)') NDIM+1
      FMT='(1X,'//CFMT//'F12.6,3I4,I5)'
C
C
      MKNOT=1
      MSET =1
C
C...OBJECT IS A SURFACE
C
      IF(NPV.EQ.2) THEN
C
C...NO OF CONTROL POINT IN T AND S
C
        NLNS=INT(CARY(5))
        NPTS=INT(CARY(6))
C
        NCTRP=NLNS*NPTS
C
C...DEGREE IN T AND S
C
        NDEGL=INT(CARY(3))-1
        NDEGP=INT(CARY(4))-1
C
C...NO OF KNOTS FOR T AND S KNOT VECTORS
C
        NKT=NDEGL+NLNS+1
        NKS=NDEGP+NPTS+1
C
C...CHECK ARRAY SIZE FOR C-ARRAY
C
        ISIZE=(NLNS*NPTS)*IDIM+NKS+NKT+8
C
        IF(ISIZE.GT.ICARY) THEN
          IERR=-3
          GOTO 999
        ENDIF
C
C...WRITE HEADER FOR AGPS FILE
C
        WRITE(JUN,20,ERR=888) NOBJ,NLNS,NPTS,NDEGL,NDEGP,MKNOT,MSET,NDIM
C
C...DEFINE LAST ELEMENT OF HEADER IN C-ARRAY
C
        KT=3*NPV+2
C
C...DEFINE LAST ELEMENT OF KNOT VECTOR FOR THE FIRST INDEPENDENT
C   VARIABLE IN C-ARRAY
C
        LKTLN=KT+NKT
C
C...DEFINE LAST ELEMENT OF KNOT VECTOR FOR THE SECOND INDEPENDENT
C   VARIABLE IN C-ARRAY.
C
        LKTPT=LKTLN+NKS
C
C...CONVERT CEOF INTO AGPS FORMAT AND WRITE AGPS FILE
C
        NCPT=0
        DO 310 L =1,NLNS
        DO 300 N =1,NPTS
        NT=L
        NS=N
        NCPT=NCPT+1
        NIK = LKTPT + (N-1)*NLNS + L
        DO 290 NC=1,IPHY
          PARRAY(NC)=CARY(NIK)
          NIK = NIK + NCTRP
  290   CONTINUE
        IF (RATNL) DENOM=CARY(NIK)
        WRITE(JUN,FMT,ERR=888) (PARRAY(I)/DENOM,I=1,NDIM),DENOM,
     +    NOBJ,NT,NS,NCPT
  300   CONTINUE
  310   CONTINUE
C
C...SCALE THE KNOT VECTOR VALUES AND WRITE TO AGPS FILE
C
C...KNOT VECTOR VALUES FOR THE SECOND INDEPENDENT VARIABLE
C
        VST=CARY(LKTLN+1)
        VEN=CARY(LKTPT)-VST
C
        WRITE(JUN,40,ERR=888) NKS,((CARY(I)-VST)/VEN,I=LKTLN+1,LKTPT)
C
C...KNOT VECTOR VALUES FOR THE FIRST INDEPENDENT VARIABLE
C
        VST=CARY(KT+1)
        VEN=CARY(LKTLN)-VST
C
        WRITE(JUN,40,ERR=888) NKT,((CARY(I)-VST)/VEN,I=KT+1,LKTLN)
C
C...OBJECT IS A CURVE
C
      ELSE
C
C...NO OF CONTROL POINT IN S
C
        NPTS=INT(CARY(4))
C
C
C...DEGREE IN S
C
        NDEGL=0
        NDEGP=INT(CARY(3))-1
C
C...NO OF KNOTS FOR S KNOT VECTORS
C
        NKT=0
        NKS=NDEGP+NPTS+1
C
C...CHECK ARRAY SIZE FOR C-ARRAY
C
        ISIZE=NPTS*IDIM+NKS+5
C
        IF(ISIZE.GT.ICARY) THEN
          IERR=-3
          GOTO 999
        ENDIF
C
C...WRITE HEADER FOR AGPS FILE
C
        WRITE(JUN,20,ERR=888) NOBJ,NLNS,NPTS,NDEGL,NDEGP,MKNOT,MSET,NDIM
C
C...DEFINE LAST ELEMENT OF HEADER IN C-ARRAY
C
        KT=3*NPV+2
C
C...DEFINE LAST ELEMENT OF KNOT VECTOR FOR THE INDEPENDENT
C   VARIABLE IN C-ARRAY
C
        LKTPT=KT+NKS
C
C...CONVERT COEF INTO AGPS FORMAT AND WRITE TO AGPS FILE
C
        DO 410 NR=1,NPTS
        NT=0
        NS=NR
        NCPT=NR
        NIK=LKTPT+NR
        DO 405 NC=1,IPHY
          PARRAY(NC)=CARY(NIK)
          NIK=NIK+NPTS
  405   CONTINUE
        IF (RATNL) DENOM=CARY(NIK)
        WRITE(JUN,FMT,ERR=888) (PARRAY(I)/DENOM,I=1,NDIM),DENOM,
     +    NOBJ,NT,NS,NCPT
  410   CONTINUE
C
C...SCALE THE KNOT VECTOR VALUES AND WRITE TO AGPS FILE
C
        VST=CARY(KT+1)
        VEN=CARY(LKTPT)-VST
C
        WRITE(JUN,40,ERR=888) NKS,((CARY(I)-VST)/VEN,I=KT+1,LKTPT)
C
      ENDIF
C
  600 CONTINUE
C
      GOTO 999
C
  888 CONTINUE
      IERR=-4
  999 CONTINUE
      CLOSE(UNIT=JUN)
      RETURN
      END

      SUBROUTINE DTSRFC(X,INCX,C,WORK,NWORK,CGAUSS,CMEAN,IER)
C
C  PURPOSE: COMPUTE GAUSSIAN AND MEANS CURVATURES OF A SPLINE SURFACE.
C
C  METHOD:  DIRECT COMPUTATION. USES NOTATION FROM CHAPTER V OF
C           O'NEILL, B., ELEMENTARY DIFFERENTIAL GEOMETRY,
C           ACADEMIC PRESS, 1966.
C
C  USAGE:  INTEGER INCX,NWORK,IER
C          DOUBLE PRECISION X(INCX,*),C(*),WORK(NWORK)
C          DOUBLE PRECISION CGAUSS,CMEAN
C          CALL DTSRFC(X,INCX,C,WORK,NWORK,CGAUSS,CMEAN,IER)
C
C  INPUTS:  X     = ARRAY OF LENGTH 2 CONTAINING PARAMETER VALUES
C                   AT WHICH TO EVALUATE THE SURFACE
C           INCX  = INCREMENT PARAMETER FOR THE ARRAY X.
C                   tHE SURFACE PARAMETERS ARE IN X(1) AND X(1+INCX).
C           C     = SPLINE DEFINITION ARRAY FOR A 3D SURFACE
C           
C  WORKING  WORK  = DOUBLE PRECISION WORK ARRAY OF LENGTH NWORK.
C  STORAGE  NWORK = TRUE LENGTH OF WORK ARRAY,
C                   NWORK .GE. 5*KMAX**2 + 3*KMAX +16, 
C                     WHERE KMAX = MAX(C(3),C(4))
C                                = MAXIMUM SPLINE ORDER.
C                              
C  OUTPUT:  CGAUSS= GAUSSIAN CURVATURE OF THE SURFACE AT X,
C                   OR SET TO A CLOBBER CONSTANT 
C                   IF THE SURFACE IS DEGENERATE THERE.
C           CMEAN = MEAN CURVATURE OF THE SURFACE AT X,
C                   OR SET TO A CLOBBER CONSTANT
C                   IF THE SURFACE IS DEGENERATE THERE.
C
C           IER   = SUCCESSS / ERROR FLAG
C                     0 => SUCCESS; UV IS NOT A SINGULAR POINT
C                   -61 => C VECTOR IS NOT A 3D SURFACE.
C                   -62 => SURFACE IS SINGULAR AT X.
C
C                   OTHERWISE, AN ERROR RETURN FROM DTNPDR, Q.V.
C
C                   WHENEVER IER .LT. 0, THE VALUES RETURNED ARE
C                   THE CLOBBER CONSTANT DTMCON(1).
C
C  SUBROUTINES CALLED:  DTMCON  DTNPDR  DTSRFN  DDOT
C
C  DATE:  17-JUL-89
C
C
C  PARAMETERS
C
      INTEGER INCX,NWORK,IER
      DOUBLE PRECISION C(*), X(INCX,2), CGAUSS, CMEAN
      DOUBLE PRECISION WORK(*)
C
C  INTERNAL VARIABLES
C
      DOUBLE PRECISION DTMCON, DDOT
      EXTERNAL DTMCON,DDOT
      DOUBLE PRECISION DU(4), DV(4), DUU(4), DUV(4), DVV(4)
      DOUBLE PRECISION E, F, G, L, M, N
      DOUBLE PRECISION VNORM(3), CROSS
      INTEGER ID(6), MODE, NEED
      CHARACTER*8 SUBNAM
      DATA SUBNAM / 'DTSRFC' /
      DATA ID / 0, 1, 1, 0, 2, 0 /
C
C  ERROR CHECKS PERFORMED BY DTSRFN.
C  NOTE THAT CURVATURES CANNOT BE COMPUTED AT SINGULAR POINTS
C  EVEN THOUGH "ROBUST" PROCEDURES MAY FIND A NORMAL THERE.
C
      CALL DTSRFN(X,INCX,C,WORK,NWORK,VNORM,CROSS,IER)
      IF(IER .LT. 0) GO TO 9400
C
      IF(IER .EQ. 0) THEN
        CALL DTNPDR(X,INCX,ID(3),C,WORK,NWORK,DU ,IER)
        IF(IER .LT. 0) GO TO 9400
        CALL DTNPDR(X,INCX,ID(1),C,WORK,NWORK,DV ,IER)
        IF(IER .LT. 0) GO TO 9400
        CALL DTNPDR(X,INCX,ID(5),C,WORK,NWORK,DUU,IER)
        IF(IER .LT. 0) GO TO 9400
        CALL DTNPDR(X,INCX,ID(2),C,WORK,NWORK,DUV,IER)
        IF(IER .LT. 0) GO TO 9400
        CALL DTNPDR(X,INCX,ID(4),C,WORK,NWORK,DVV,IER)
        IF(IER .LT. 0) GO TO 9400
C
        E = DDOT(3,DU,1,DU,1)
        F = DDOT(3,DU,1,DV,1)
        G = DDOT(3,DV,1,DV,1)
        L = DDOT(3,DUU,1,VNORM,1)
        M = DDOT(3,DUV,1,VNORM,1)
        N = DDOT(3,DVV,1,VNORM,1)
C
        CGAUSS = (L*N - M**2) / CROSS
        CMEAN  = (G*L + E*N - 2.D0*F*M) / (2.D0*CROSS)
C
      ELSE
        IER = -62
        GO TO 9300
      ENDIF
C
C  NORMAL EXIT
C
      RETURN
C
C  ERROR EXITS
C
 9300 CONTINUE
      MODE = 3
      GO TO 9900
 9400 CONTINUE
      MODE = 4
 9900 CONTINUE
      CALL DTERR(MODE,SUBNAM,IER,NEED)
      CGAUSS = DTMCON(1)
      CMEAN  = DTMCON(1)
      RETURN
C
      END
